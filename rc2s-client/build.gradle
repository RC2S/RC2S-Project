apply plugin: 'application'

mainClassName = 'com.rc2s.client.Main'

repositories {
    flatDir {
        dirs '../rc2s-ejb/build/libs'
    }
}

dependencies {
    compile project(':rc2s-common')
    compile project(':rc2s-annotations')
    compile name: "interfaces-$version"
    compile group: 'fish.payara.extras', name: 'payara-embedded-all', version: '4.1.1.161'
}

project.tasks.compileJava.dependsOn(
	rootProject.getSubprojects().findAll {
        subproject -> subproject.name != project.name 
	}.collect { 
        subproject -> "${subproject.getPath()}:build"
	}
)

jar {
    manifest {
        attributes(
            "Implementation-Title" : project.name,
            "Implementation-Version" : version,
            "Main-Class" : mainClassName,
            //"Class-Path" : configurations.compile.collect{it.getName()}.join(' '),
            "Permissions": "all-permissions",
            "Application-Name" : project.name
        )
    }
    
    doLast {
        tasks.copyLibs.execute();
        tasks.signAll.execute();
        tasks.generateJnlp.execute();
    }
}

/**
 * JNLP
 */
task copyLibs(type: Copy) {
    from("${project.buildDir}/libs")
    from configurations.runtime
    into("${project.buildDir}/jnlp/build")
    include('*.jar')
}

task signAll(dependsOn: [copyLibs]) << {
    println("Signing Libs")
    new File("${project.buildDir}/jnlp/libs").mkdirs()
    def libFiles = files { file("${project.buildDir}/jnlp/build").listFiles() }
    libFiles.each {
        ant.signjar(
            destDir: "${project.buildDir}/jnlp/libs",
            alias: 'RC2S',
            jar: it,
            keystore: "${project.projectDir}/src/main/jnlp/keys/RC2S.jks",
            storepass: 'P@ssword1234',
            preservelastmodified: 'true'
        )
    }
    delete "${project.buildDir}/jnlp/build"
}

task generateJnlp(dependsOn: [signAll]) << {
    println("Generating JNLP")
    File jnlpTemplateFile = new File("${project.projectDir}/src/main/jnlp/descriptors/rc2s-${env}.jnlp")
    def root = new XmlParser().parse(jnlpTemplateFile)
    def jnlpFileName = "rc2s-client-${env}-${version}.jnlp"
    
    // Setting values in the jnlp template
    
    // Racine
    root.@codebase = 'file://' + new File("${project.buildDir}/jnlp").absolutePath
    //root.@codebase = 'https://someserver.com/some/path'
    root.@href = jnlpFileName
    
    // Initialisation des informations
    def information = root.information[0]
    def title = information.title[0]
    def vendor = information.vendor[0]
    title.setValue("RC2S-Client")
    vendor.setValue("RC2S")
    
    // Initialisation des ressources (main JAR + libs)
    def resource = root.resources[0]
    def j2se = resource.j2se[0]
    j2se.@version = "1.8+"
    
    def mainJar = "rc2s-client-${version}.jar"
    def libs = files { file("${project.buildDir}/jnlp/libs").listFiles() }
    libs.each {
        def resourceValues = [href: "libs/" + it.name]
        if (mainJar == it.name) {
            resourceValues = [href: "libs/" + it.name, main: 'true']
        }
        resource.appendNode('jar', resourceValues)
    }
    
    // On définit la class principale
    def applicationDesc = root.'application-desc'[0]
    applicationDesc.'@main-class' = mainClassName
    
    // Écriture du nouveau contenu dans un nouveau fichier JNLP
    File jnlpFile = new File("${project.buildDir}/jnlp/" + jnlpFileName)
    new XmlNodePrinter(new PrintWriter(jnlpFile)).print(root)
}